use std::collections::HashMap;
use std::path::Path;

use glam::{DVec4, DMat4, DVec3};

use super::{SceneBuilder, Props, Value};

grammar(builder: &mut SceneBuilder);

pub TopLevel = Statement*;

Statement: () = {
    "Include" <String> => builder.include(Path::new(<>)),
    "Import" <String> => builder.include(Path::new(<>)),
    "WorldBegin" => builder.world_begin(),
    "AttributeBegin" => builder.push(),
    "AttributeEnd" => builder.pop(),

    "ObjectBegin" <String> => builder.begin_object(<>),
    "ObjectEnd" => builder.end_object(),
    "ObjectInstance" <String> => builder.instance_object(<>),

    "Identity" => builder.identity(),
    "LookAt" <MaybeBracketed<(Vec3 Vec3 Vec3)>> => builder.look_at(<>),
    "Rotate" <MaybeBracketed<(Number Vec3)>> => builder.rotate(<>),
    "Translate" <MaybeBracketed<Vec3>> => builder.translate(<>),
    "Scale" <MaybeBracketed<Vec3>> => builder.scale(<>),
    "Transform" <MaybeBracketed<Mat4>> => builder.set_transform(<>),
    "ConcatTransform" <MaybeBracketed<Mat4>> => builder.apply_transform(<>),

    "Camera" <String> <Properties> => builder.camera(<>),

    "Shape" <ty:String> <props:Properties> => match ty {
        "sphere" => builder.sphere(props),
        "trianglemesh" => builder.triangle_mesh(props),
        "loopsubdiv" => builder.loop_subdivision_surface(props),
        "plymesh" => builder.plymesh(props),
        _ => builder.unrecognized_shape(ty),
    },

    "Texture" <name:String> <kind:String> <ty:String> <props:Properties> => match ty {
        "constant" => builder.constant_texture(name, props),
        "scale" => builder.scale_texture(name, props),
        "mix" => builder.mix_texture(name, props),
        "checkerboard" => builder.checkerboard_texture(name, props),
        "imagemap" => builder.image_texture(name, kind, props),
        _ => builder.unrecognized_texture(ty),
    },

    "Material" <String> <Properties> => builder.material(<>),
    "MakeNamedMaterial" <String> <Properties> => builder.make_named_material(<>),
    "NamedMaterial" <String> => builder.named_material(<>),

    "LightSource" <ty:String> <props:Properties> => match ty {
        "infinite" => builder.infinite_light(props),
        _ => builder.unrecognized_light(ty),
    },
    "AreaLightSource" <ty:String> <props:Properties> => match ty {
        "diffuse" => builder.diffuse_area_light(props),
        _ => builder.unrecognized_area_light(ty),
    },

    Ident ! => builder.unrecognized(<>),
}

Properties: Props<'input> = (PropName Values)* => Props {
    map: <>.into_iter().map(|((ty, k), v)| (k, (ty, v))).collect()
};

Values: Vec<Value<'input>> = {
    Value => vec![<>],
    "[" <Value*> "]" => <>,
}

Value: Value<'input> = {
    String => Value::String(<>),
    Number => Value::Number(<>),
    "true" => Value::Boolean(true),
    "false" => Value::Boolean(false),
}

MaybeBracketed<T> = {
    "[" <T> "]",
    T
}

PropName: (&'input str, &'input str) = String => <>.split_once(' ').unwrap();

Vec3: DVec3 = Number Number Number => DVec3::new(<>);

Vec4: DVec4 = Number Number Number Number => DVec4::new(<>);

Mat4: DMat4 = Vec4 Vec4 Vec4 Vec4 => DMat4::from_cols(<>);

Number: f64 = r"-?(\d+(\.\d*)?|\.\d+)(e-?\d+)?" => <>.parse().unwrap();

String: &'input str = r#""[^"]*""# => <>.trim_matches('"');

Ident: &'input str = r"[a-zA-Z]\w*";

match {
    r"#[^\n]*" => {},
    r"\s*" => {},
} else {
    _
}
